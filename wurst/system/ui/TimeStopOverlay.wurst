package TimeStopOverlay

/*
 * Fullscreen overlay that blocks mouse clicks and keyboard inputs.
 *
 * The frame is created once at map init so it can be shown
 * or hidden for any player when time stop effects are active.
 */

import FramehandleNames
import ClosureTimers

public framehandle CONTROL_BLOCKER_FRAME = null

function createControlBlockerFrame()
	if CONTROL_BLOCKER_FRAME != null
		return

	CONTROL_BLOCKER_FRAME = createFrame(FramehandleTypeNames.editbox, "ControlBlocker", getFrame("ConsoleUIBackdrop", 0), FramehandleNames.iconButtonTemplate, 2)
	..clearAllPoints()
	..setAbsPoint(FRAMEPOINT_TOPLEFT, WHOLE_SCREEN_TOPLEFT)
	..setAbsPoint(FRAMEPOINT_BOTTOMRIGHT, WHOLE_SCREEN_BOTTOMRIGHT)
	..setLevel(1)
	..setAlpha(0)
	..unfocus()
	..setFocus(false)
	..hide()

public function player.disableControl()
	if CONTROL_BLOCKER_FRAME == null
		createControlBlockerFrame()
	if CONTROL_BLOCKER_FRAME.isVisible(this)
		return
	if GetLocalPlayer() == this
		CONTROL_BLOCKER_FRAME
		..setEnabled(true)
		..show(this)
		..setFocus(this, true)

public function player.enableControl()
	if CONTROL_BLOCKER_FRAME == null
		return
	if CONTROL_BLOCKER_FRAME.isVisible(this) and GetLocalPlayer() == this
		CONTROL_BLOCKER_FRAME
		..setEnabled(false)
		..setFocus(this, false)
		..unfocus()
		..hide()

init
	nullTimer() ->
		createControlBlockerFrame()