package DraftInitialize

import DraftAPI
import TableLayout
import TableLayoutExt
import GridTableWrapper
import DraftHeroData
import ClosureTimers

function setupCategoryButtons()
    for i = 1 to MAX_CATEGORIES
        categoryBtns[i] = imgBtn(catTex[i])
            ..setWidth(categoryPageSize)
            ..setHeight(categoryPageSize)
        categoryBtnIcons[i] = getButtonIcon(categoryBtns[i])

function setupPageButtons()
    for i = 1 to MAX_PAGES
        pageBtns[i] = btn(i.toString())
            ..setWidth(categoryPageSize)
            ..setHeight(categoryPageSize)

function draftScreen()
    hideOriginFrames()
    getFrame("ConsoleUIBackdrop", 0).setSize(0, 0.0001)

    let parent = createFrame("FRAME", "DraftParent", getFrame("ConsoleUIBackdrop", 0), "", 0)
    defaultFrameParent = parent

    let team1Bans = new TableLayout(0.3, 0.0675)
    ..row()..center()
    ..add(imgBtn("Strength_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Strength_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Strength_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Strength_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Strength_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))

    let team2Bans = new TableLayout(0.3, 0.0675)
    ..row()..center()
    ..add(imgBtn("Intelligence_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Intelligence_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Intelligence_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Intelligence_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))..padRight(0.005)
    ..add(imgBtn("Intelligence_enabled.dds")..setWidth(banIconSize)..setHeight(banIconSize))
    
    let timerTable = new TableLayout(0.15, 0.0675)
        ..row()..center()
        ..add(img("number_6.dds", 0.0400, 0.0400, true))
        ..add(img("number_0.dds", 0.0400, 0.0400, true))

    let categoryPageButtonsTable = new TableLayout(0.2, 0.0625)
        ..row()
        ..add(categoryBtns[1])..add(categoryBtns[2])..add(categoryBtns[3])..add(categoryBtns[4])
        ..add(categoryBtns[5])..add(categoryBtns[6])..add(categoryBtns[7])..add(categoryBtns[8])
        ..add(h1(""))..growX()
        ..add(pageBtns[1])..add(pageBtns[2])..add(pageBtns[3])

    let team1PickedTable = new TableLayout(0.125, 0.5)
        ..row()..add(p("Header L"))..growX()

    let team2PickedTable = new TableLayout(0.125, 0.5)
        ..row()..add(p("Header R"))..growX()

    heroGrid = new GridCell(0.4, 0.220, 10, heroIconSize, heroIconSize, 0.006)

    let mainTable = new TableLayout(0.8, 0.5)
        ..row()..center()
        ..add(categoryPageButtonsTable.createFramedContainer(parent))..growX()
        ..row()
        ..add(heroGrid.createFramedContainer(parent))..growX()

    let rootTable = new TableLayout(1.0, 0.6)
        ..row()..center()
        ..add(team1Bans.createContainerEx(parent))..padLeft(0.05)
        ..add(timerTable.createContainerEx(parent))
        ..add(team2Bans.createContainerEx(parent))..padRight(0.05)
        ..row()..center()
        ..add(team1PickedTable.createContainerEx(parent))
        ..add(mainTable.createContainerEx(parent))..growX()
        ..add(team2PickedTable.createContainerEx(parent))

    let rootFrame = rootTable.createFrame()
        ..clearAllPoints()
        ..setAbsPoint(FRAMEPOINT_CENTER, 0.4, 0.3)
    rootTable.applyToCascade(rootFrame)

    wireCategoryButtons()
    wirePageButtons()

    let pidLocal = GetPlayerId(GetLocalPlayer())
    let st = getDraftUI(pidLocal)
    st.currentPage = 1
    shownNow.clear()
    refreshGrid(pidLocal)

    defaultFrameParent = GAME_UI

init
    string array categoryImgNames
    categoryImgNames[0] = "Strength"
    categoryImgNames[1] = "Agility"
    categoryImgNames[2] = "Intelligence"
    for i = 1 to MAX_CATEGORIES
        let name = categoryImgNames[((i - 1) % 3).toInt()]
        catTex[i] = name + ".dds"
        catTexOn[i] = name + "_enabled.dds"
        catTexDisabled[i] = name + "_disabled.dds"
    setupCategoryButtons()
    setupPageButtons()

    doAfter(0.25) ->
        defineHeroes()
        sortAllHeroesOnce()

    doAfter(0.5) ->
        draftScreen()