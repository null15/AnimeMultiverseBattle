package DraftInitialize

import DraftAPI
import TableLayout
import TableLayoutExt
import GridTableWrapper
import DraftHeroData
import ClosureTimers

function setupCategoryButtons()
    string array categoryName
    categoryName[1] = "Strength.dds"
    categoryName[2] = "Agility.dds"
    categoryName[3] = "Intelligence.dds"
    categoryName[4] = "Strength.dds"
    categoryName[5] = "Strength.dds"
    categoryName[6] = "Strength.dds"
    categoryName[7] = "Strength.dds"
    categoryName[8] = "Strength.dds"

    for i = 1 to MAX_CATEGORIES
        categoryBtns[i] = imgBtn(categoryName[i])
            ..setWidth(categoryPageSize)
            ..setHeight(categoryPageSize)
        registerCategoryLabel(i, categoryName[i])

function setupPageButtons()
    for i = 1 to MAX_PAGES
        pageBtns[i] = btn(i.toString())
            ..setWidth(categoryPageSize)
            ..setHeight(categoryPageSize)

function draftScreen()
    hideOriginFrames()
    getFrame("ConsoleUIBackdrop", 0).setSize(0, 0.0001)

    let parent = createFrame("FRAME", "DraftParent", getFrame("ConsoleUIBackdrop", 0), "", 0)
    defaultFrameParent = parent

    buildHeroButtons()
    setupHeroButtons()

    let timerTable = new TableLayout(0.1, 0.05)
        ..row()..add(p("Timer"))..growX()

    let categoryPageButtonsTable = new TableLayout(0.2, 0.07)
        ..row()
        ..add(categoryBtns[1])..add(categoryBtns[2])..add(categoryBtns[3])..add(categoryBtns[4])
        ..add(categoryBtns[5])..add(categoryBtns[6])..add(categoryBtns[7])..add(categoryBtns[8])
        ..add(h1(""))..growX()
        ..add(pageBtns[1])..add(pageBtns[2])..add(pageBtns[3])

    let team1PickedTable = new TableLayout(0.125, 0.5)
        ..row()..add(p("Header L"))..growX()

    let team2PickedTable = new TableLayout(0.125, 0.5)
        ..row()..add(p("Header R"))..growX()

    heroGrid = new GridCell(0.4, 0.25, 10, heroIconSize, heroIconSize, 0.006)

    let mainTable = new TableLayout(0.8, 0.5)
        ..row()..center()
        ..add(categoryPageButtonsTable.createFramedContainer(parent))..growX()
        ..row()
        ..add(heroGrid.createFramedContainer(parent))..growX()

    let rootTable = new TableLayout(1.0, 0.6)
        ..row()..center()
        ..add(timerTable.createFramedContainer(parent))..padBot(0.005)
        ..row()..center()
        ..add(team1PickedTable.createFramedContainer(parent))
        ..add(mainTable.createFramedContainer(parent))..growX()
        ..add(team2PickedTable.createFramedContainer(parent))

    let rootFrame = rootTable.createFrame()
        ..clearAllPoints()
        ..setAbsPoint(FRAMEPOINT_CENTER, 0.4, 0.3)
    rootTable.applyToCascade(rootFrame)

    wireCategoryButtons()
    wirePageButtons()

    let pidLocal = GetPlayerId(GetLocalPlayer())
    let st = getOrCreateState(pidLocal)
    st.currentPage = 1
    shownNow.clear()
    refreshGrid(pidLocal)
    updateCategoryButtonsVisual(pidLocal) // initial ON/TRUE visuals

    defaultFrameParent = GAME_UI

init
    setupCategoryButtons()
    setupPageButtons()
    defineHeroes()

    // Slight delay before creating draft UI, avoids any frame issues since creating frames at init can cause bugs
    doAfter(1) ->
        draftScreen()