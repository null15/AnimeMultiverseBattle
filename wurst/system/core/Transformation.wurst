package Transformation

import ClosureTimers
import HashMap
import unitDefinitions
import Entity

public class Transformation
	unit actor
	int originalSkin
	IterableMap<int,int> swaps
	unit bar
	CallbackSingle cb
	CallbackCounted cbPeriodicBar

	construct(unit actor, int newSkin, IterableMap<int,int> swaps, real duration)
		this.actor = actor
		this.swaps = swaps
		this.originalSkin = BlzGetUnitSkin(this.actor)

		// Unit Skin & Abilities
		BlzSetUnitSkin(this.actor, newSkin)
		swaps.forEach() (int baseAbil, int transAbil) ->
			this.actor.removeAbility(baseAbil)
			this.actor.addAbility(transAbil)

		// Progress Bar
		this.bar = createUnit(this.actor.getOwner(), PROGRESS_BAR_DUMMY, this.actor.getEntity() != null ? this.actor.getEntity().getPos().add(0, 0, 400) : this.actor.getPos3Real().add(0, 0, 400), angle(270))
		..setVertexColor(this.actor.getOwner().getColor().toColor())
		..setPosReal(this.actor.getEntity() != null ? this.actor.getEntity().getPos().add(0, 0, 300) : this.actor.getPos3Real().add(0, 0, 300))
		..setAnimation(100)

		// Timer Stuff
		this.cbPeriodicBar = doPeriodicallyTimed(0.032, duration) (CallbackCounted cb) ->
			let progressLeft = (1.0 - cb.progress())
			let value        = (100.0 * progressLeft).toInt()
			this.bar
			..setPosReal(this.actor.getEntity() != null ? this.actor.getEntity().getPos().add(0, 0, 300) : this.actor.getPos3Real().add(0, 0, 300))
			..setAnimation(value)
			if cb.isLast()
				this.bar.setAnimation(0)
				this.bar.remove()
				destroy this.cbPeriodicBar

		this.cb = doAfter(duration) ->
			finish()

	function finish()
		this.swaps.forEach() (int baseAbil, int transAbil) ->
			this.actor.removeAbility(transAbil)
			this.actor.addAbility(baseAbil)
		BlzSetUnitSkin(this.actor, this.originalSkin)
		if this.bar != null
			this.bar.remove()
			this.bar = null
		this.cb = null
		this.cbPeriodicBar = null
		destroy this

	ondestroy
		if this.cb != null
			destroy this.cb
		if this.cbPeriodicBar != null
			destroy this.cbPeriodicBar
		if this.bar != null
			this.bar.remove()
		destroy this.swaps