package Pull

import ClosureTimers

constant real PULL_PERIOD = 1./40.

class PullEffect
    unit target
    vec2 direction
    real speed
    real traveled = 0.
    real maxDistance
    CallbackPeriodic cb

    construct(unit target, vec2 source, real distance, real duration)
        this.target = target
        direction = source - target.getPos()
        direction = direction.norm()
        maxDistance = distance
        speed = distance / duration
        cb = doPeriodically(PULL_PERIOD, _ -> move())

    function move()
        if traveled >= maxDistance
            destroy this
        else
            let stp = speed * PULL_PERIOD
            traveled += stp
            let newPos = target.getPos() + direction * stp
            target.setPos(newPos)

    ondestroy
        if cb != null
            destroy cb

public function pullUnit(unit target, vec2 source, real distance, real duration)
    new PullEffect(target, source, distance, duration)