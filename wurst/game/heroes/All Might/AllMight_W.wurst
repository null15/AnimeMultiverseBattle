package AllMight_W

import Missiles
import ClosureTimers
import ClosureForGroups
import LinkedList
import abilityDefinitions
import RegisterEvents

function onCast()
	if GetSpellAbilityId() == TIME_STOP
		let caster = GetTriggerUnit()
		let center = vec2(GetSpellTargetX(), GetSpellTargetY())
		let radius = 600.
		let pausedUnits = new LinkedList<unit>()
		let pausedMissiles = new LinkedList<Missiles>()

		forUnitsInRange(center, radius) (unit u) ->
			if u != caster
				PauseUnit(u, true)
				pausedUnits.add(u)

		for miss in Missiles.collection
			if miss.getPos().toVec2().distanceTo(center) <= radius and not miss.isPaused()
				miss.pause(true)
				pausedMissiles.add(miss)

		doAfter(3.) ->
			for u in pausedUnits
				PauseUnit(u, false)
			for miss in pausedMissiles
				miss.pause(false)
			destroy pausedUnits
			destroy pausedMissiles

init
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> onCast())
