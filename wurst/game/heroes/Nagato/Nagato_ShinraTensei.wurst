package Nagato_ShinraTensei

import objectIDs
import Knockback
import Missiles
import ClosureForGroups
import ClosureTimers
import HashMap
import HashSet
import EntityManagement
import SoundUtils
import SoundSystem

constant HashMap<unit, ShinraTensei> active = new HashMap<unit, ShinraTensei>()
constant real MAX_Z_VEL = 600.
constant SoundDefinition snd_ShinraTensei = new SoundDefinition("Nagato\\Shinra Tensei\\ShinraTensei.mp3", false, true)

class ShinraTensei
	unit caster
	real remaining
	CallbackPeriodic cb
	HashSet<unit> affected
	effect shinraTensei
	effect shinraTenseiDirt
	effect dustWave
	effect shinraTenseiBlast

	construct(unit caster)
		this.caster = caster
		this.remaining = 2.
		this.affected = new HashSet<unit>
		this.shinraTensei = addEffect("Nagato\\Shinra Tensei\\ShinraTensei.mdx", this.caster.getEntity().getPos().add(0, 0, -300))..setScale(1.5)
		this.shinraTenseiDirt = addEffect("Nagato\\Shinra Tensei\\ShinraTenseiDirt.mdx", this.caster.getEntity().getPos().add(0, 0, 0))..setScale(0.1)..setTimeScale(0.5)..playAnimation(ANIM_TYPE_BIRTH)
		this.dustWave = addEffect("Nagato\\Shinra Tensei\\DustWaveVersion2.mdx", this.caster.getEntity().getPos())..playAnimation(ANIM_TYPE_BIRTH)..setScale(4)
		this.shinraTenseiBlast = addEffect("Nagato\\Shinra Tensei\\ShinraTenseiBlast.mdx", this.caster.getEntity().getPos())..playAnimation(ANIM_TYPE_STAND)..setScale(2)
		playSound(snd_ShinraTensei, this.caster.getEntity().getPos(), 700)
		this.cb = doPeriodically(0.1) (CallbackPeriodic c) ->
			this.tick()

	function tick()
		if caster == null or not caster.isAlive()
			stop()
			return
		this.remaining -= 0.1
		if this.remaining <= 0
			stop()
			return
		let center = caster.getPos()
		this.shinraTensei.setPos(this.caster.getEntity().getPos().add(0, 0, -300))
		this.shinraTenseiDirt.setPos(this.caster.getEntity().getPos().add(0, 0, 0))
		this.dustWave.setPos(this.caster.getEntity().getPos())
		this.shinraTenseiBlast.setPos(this.caster.getEntity().getPos())
		forUnitsInRange(center, 400.) (unit u) ->
			if caster.isEnemyOf(u)
				caster.damageTarget(u, 1., ATTACK_TYPE_HERO)
				let angle = center.angleTo(u.getPos())
				if affected.has(u)
					// AI did the math
					let v = Knockback3.getVel(u)
					let vz = (v.z / ANIMATION_PERIOD).clamp(-MAX_Z_VEL, MAX_Z_VEL)
					let speed = SquareRoot(800. * 800. + vz * vz)
					let air = vz.atan2(800.).fromRad()
					Knockback3.setVel(u, speed, angle, air)
				else
					Knockback3.setVel(u, 800., angle, (30.).fromDeg())
					affected.add(u)
				this.checkCollision(u)
		for m in Missiles.collection
			if m.owner != caster.getOwner()
				let mPos = m.getPos().toVec2()
				if mPos.distanceTo(center) <= 400.
					let target = mPos + (mPos - center)
					m.owner = this.caster.getOwner()
					m.deflect(target)

	function checkCollision(unit u)
		if not u.getPos().isTerrainWalkable()
			this.applyCollision(u)
			return
		forUnitsInRange(u.getPos(), 128.) (unit rod) ->
			if rod.getTypeId() == NAGATO_UZUMAKI_ROD
				this.applyCollision(u)

	function applyCollision(unit u)
		caster.damageTarget(u, 5., ATTACK_TYPE_HERO)
		let baseSpeed = u.getMoveSpeed()
		u.setMoveSpeed(baseSpeed * 0.6)
		doAfter(2.) ->
			if u != null
				u.setMoveSpeed(baseSpeed)

	function stop()
		if cb != null
			destroy cb
			cb = null
		active.remove(caster)
		if affected != null
			destroy affected
			affected = null
		if this.shinraTensei != null
			this.shinraTensei..setPos(vec2(-99999, -99999))..destr()
		if this.shinraTenseiDirt != null
			this.shinraTenseiDirt..setPos(vec2(-99999, -99999))..destr()
		if this.dustWave != null
			this.dustWave..setPos(vec2(-99999, -99999))..destr()
		if this.shinraTenseiBlast != null
			this.shinraTenseiBlast..setPos(vec2(-99999, -99999))..destr()
		destroy this

function onCast()
	if GetSpellAbilityId() == NAGATO_SHINRA_TENSEI
		let caster = GetTriggerUnit()
		let inst = new ShinraTensei(caster)
		active.put(caster, inst)

function onStop()
	if GetSpellAbilityId() == NAGATO_SHINRA_TENSEI
		let caster = GetTriggerUnit()
		if active.has(caster)
			active.get(caster).stop()

init
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> onCast())
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_ENDCAST, () -> onStop())
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_FINISH, () -> onStop())